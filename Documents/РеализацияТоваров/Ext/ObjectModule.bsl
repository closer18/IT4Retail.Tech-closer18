
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = Товары.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиТоваров.Записать();
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТоваровТовары.Количество) КАК КоличествоВДокументе,
		|	РеализацияТоваровТовары.СрокГодности КАК ПартияВДокументе
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровТовары.Номенклатура,
		|	РеализацияТоваровТовары.СрокГодности
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(ВТ_Товары.Номенклатура) КАК НоменклатураПредставление,
		|	ВТ_Товары.КоличествоВДокументе КАК КоличествоВДокументе,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ОстаткиТоваровОстатки.Партия КАК Партия
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиТоваровОстатки
		|		ПО ВТ_Товары.Номенклатура = ОстаткиТоваровОстатки.Номенклатура
		|			И (ВТ_Товары.ПартияВДокументе <> ДАТАВРЕМЯ(1, 1, 1)
		|					И ОстаткиТоваровОстатки.Партия = ВТ_Товары.ПартияВДокументе
		|				ИЛИ ВТ_Товары.ПартияВДокументе = ДАТАВРЕМЯ(1, 1, 1))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Партия
		|ИТОГИ
		|	МАКСИМУМ(КоличествоВДокументе),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаТовары = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаТовары.Следующий() Цикл
		Если ВыборкаТовары.КоличествоВДокументе > ВыборкаТовары.КоличествоОстаток Тогда
			Отказ = Истина;
			
			Недостаток = ВыборкаТовары.КоличествоВДокументе - ВыборкаТовары.КоличествоОстаток;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
				ВыборкаТовары.НоменклатураПредставление, Недостаток);
			Сообщение.Сообщить();
			
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоСписать = ВыборкаТовары.КоличествоВДокументе;
		
		ВыборкаПартии = ВыборкаТовары.Выбрать();
		Пока ВыборкаПартии.Следующий() И КоличествоСписать > 0 Цикл
			Количество = Мин(ВыборкаПартии.КоличествоВДокументе, ВыборкаПартии.КоличествоОстаток);
			Если ВыборкаПартии.КоличествоВДокументе > ВыборкаПартии.КоличествоОстаток Тогда
				Отказ = Истина;
				
				Недостаток = ВыборкаПартии.КоличествоВДокументе - ВыборкаПартии.КоличествоОстаток;
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 по партии от %3",
					ВыборкаПартии.НоменклатураПредставление, Недостаток, Формат(ВыборкаПартии.Партия, "ДФ=dd.MM.yyyy"));
				Сообщение.Сообщить();
				
				Прервать;
			КонецЕсли;
			
			Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаТовары.Номенклатура;
			Движение.Партия = ВыборкаПартии.Партия;
			Движение.Количество = Количество;
			
			КоличествоСписать = КоличествоСписать - Количество;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры


