
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = Товары.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТоваровТовары.Количество) КАК КоличествоВДокументе,
		|	РеализацияТоваровТовары.СрокГодности КАК ПартияВДокументе
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровТовары.Номенклатура,
		|	РеализацияТоваровТовары.СрокГодности
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.КоличествоВДокументе КАК КоличествоВДокументе,
		|	ВТ_Товары.ПартияВДокументе КАК ПартияВДокументе
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ОстаткиТоваров.Очистить();
	
	ВыборкаТовары = РезультатЗапроса.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
			Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаТовары.Номенклатура;
			Движение.Партия = ВыборкаТовары.ПартияВДокументе;
			Движение.Количество = ВыборкаТовары.КоличествоВДокументе;
	КонецЦикла;
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровОстатки.Партия КАК Партия,
		|	-ОстаткиТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПРЕДСТАВЛЕНИЕ(ОстаткиТоваровОстатки.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					ВТ_Товары.Номенклатура КАК Номенклатура
		|				ИЗ
		|					ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиТоваровОстатки
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		ВыборкаОшибки = РезультатЗапроса.Выбрать();
		Пока ВыборкаОшибки.Следующий() Цикл
			
			Если ВыборкаОшибки.Партия = Дата(1,1,1) Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Отсутствует партия для списания товара %1",
					ВыборкаОшибки.НоменклатураПредставление);
				Сообщение.Сообщить();
			Иначе
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 по партии от %3",
				ВыборкаОшибки.НоменклатураПредставление, ВыборкаОшибки.КоличествоОстаток, Формат(ВыборкаОшибки.Партия, "ДФ=dd.MM.yyyy"));
				Сообщение.Сообщить();
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры


