
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаДокумента = Товары.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.Продажи.Записать();
	Движения.ВзаиморасчетыСКонтрагентами.Записать();
	Движения.ОстаткиТоваров.Записать();
	Движения.Продажи.Записывать = Истина;
	Движения.ВзаиморасчетыСКонтрагентами.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровТовары.Ссылка.Контрагент КАК Контрагент,
		|	РеализацияТоваровТовары.Ссылка.Договор КАК Договор,
		|	СУММА(РеализацияТоваровТовары.Сумма) КАК Сумма
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровТовары.Ссылка.Контрагент,
		|	РеализацияТоваровТовары.Ссылка.Договор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТоваровТовары.Количество) КАК КоличествоВДокументе,
		|	СУММА(РеализацияТоваровТовары.Сумма) КАК СуммаВДокументе,
		|	РеализацияТоваровТовары.Ссылка.Контрагент КАК Контрагент
		|ПОМЕСТИТЬ ВТ_РеализацияТоваров
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровТовары.Номенклатура,
		|	РеализацияТоваровТовары.Ссылка.Контрагент
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РеализацияТоваров.Контрагент КАК Контрагент,
		|	ВТ_РеализацияТоваров.Номенклатура КАК Номенклатура,
		|	ВТ_РеализацияТоваров.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ВТ_РеализацияТоваров.КоличествоВДокументе КАК КоличествоВДокументе,
		|	ВТ_РеализацияТоваров.СуммаВДокументе КАК СуммаВДокументе,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток
		|ИЗ
		|	ВТ_РеализацияТоваров КАК ВТ_РеализацияТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
		|				&МоментВремени,
		|				Склад = &Склад
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							ВТ_РеализацияТоваров.Номенклатура КАК Номенклатура
		|						ИЗ
		|							ВТ_РеализацияТоваров КАК ВТ_РеализацияТоваров)) КАК ОстаткиТоваровОстатки
		|		ПО ВТ_РеализацияТоваров.Номенклатура = ОстаткиТоваровОстатки.Номенклатура";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоВДокументе > Выборка.КоличествоОстаток Тогда
			Отказ = Истина;
			Недостаток = Выборка.КоличествоВДокументе - Выборка.КоличествоОстаток;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара %1 в количестве %2", Выборка.НоменклатураПредставление, Недостаток);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Себестоимость = 0;
		Если Выборка.КоличествоВДокументе = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СебестоимостьОстаток;
		Иначе
			Себестоимость = Выборка.СебестоимостьОстаток / Выборка.КоличествоОстаток * Выборка.КоличествоВДокументе;
		КонецЕсли;
		
		// Регистр ОстаткиТоваров Расход
		Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.КоличествоВДокументе;
		Движение.Себестоимость = Себестоимость;
		
		Для Каждого ДвиженияОстатки Из Движения.ОстаткиТоваров Цикл
			
			Если ДвиженияОстатки.Номенклатура = Выборка.Номенклатура Тогда
				
				// Регистр Продажи Обороты
				Движение = Движения.Продажи.Добавить();
				Движение.Контрагент = Выборка.Контрагент;
				Движение.Номенклатура = Выборка.Номенклатура;
				Движение.Период = Дата;
				Движение.Выручка = Выборка.СуммаВДокументе;
				Движение.Себестоимость = ДвиженияОстатки.Себестоимость;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Выборка = МассивРезультатов[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// регистр ВзаиморасчетыСКонтрагентами Приход
		Движение = Движения.ВзаиморасчетыСКонтрагентами.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Выборка.Контрагент;
		Движение.Договор = Выборка.Договор;
		Движение.Сумма = Выборка.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры


