
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Статус = ПрочитатьСтатус();
	ПрочитатьИсториюСтатусов();
КонецПроцедуры

&НаСервере
Функция ПрочитатьСтатус()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыКонтрагентовСрезПоследних.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.СтатусыКонтрагентов.СрезПоследних(&Период, Контрагент = &Контрагент) КАК СтатусыКонтрагентовСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатусыКонтрагентовСрезПоследних.Период";
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Контрагент", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Статус;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

&НаСервере
Процедура ЗаписатьСтатус(ОбъектСсылка)
	
	// Проверяем - изменил ли пользователь статус?
	Если Статус <> ПрочитатьСтатус() Тогда
		
		МенеджерЗаписи = РегистрыСведений.СтатусыКонтрагентов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = ТекущаяДатаСеанса();
		МенеджерЗаписи.Контрагент = ОбъектСсылка;
		МенеджерЗаписи.Статус = Статус;
		МенеджерЗаписи.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ЗаписатьСтатус(ТекущийОбъект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИсториюСтатусов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатусыКонтрагентов.Период КАК Период,
		|	СтатусыКонтрагентов.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.СтатусыКонтрагентов КАК СтатусыКонтрагентов
		|ГДЕ
		|	СтатусыКонтрагентов.Контрагент = &Контрагент";
	Запрос.УстановитьПараметр("Контрагент", Объект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	ИсторияСтатусов.Загрузить(РезультатЗапроса.Выгрузить());

КонецПроцедуры






